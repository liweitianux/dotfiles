##
## ~/.tcshrc
##
## Aaron LI
## 2011-06-30
##
## 2017-10-30: Cleanups; add new keybindings
##


### PATH ###
if ( -d ~/bin ) then
    set path = ( ~/bin $path )
endif

## admin
groups | grep -qE '\b(wheel|adm|sudo)\b'
if ( $status == 0 ) then
    set path = ( $path /sbin /usr/sbin /usr/local/sbin )
endif


### Environments ###
# colors for 'ls'
setenv CLICOLOR     yes
setenv LSCOLORS     ExGxFxdxCxegedabagExEx

# Misc
setenv PAGER less
setenv LESS '-ceiMs'
setenv EDITOR vim
setenv VISUAL vim


## skip remaining setup if not an interactive shell
if ( $?USER == 0 || $?prompt == 0 ) exit


### color code ###
# 1 for brighter colors
# 4 for underlined text
# 5 for flashing text
# 30 for black foreground
# 31 for red foreground
# 32 for green foreground
# 33 for yellow (or brown) foreground
# 34 for blue foreground
# 35 for purple foreground
# 36 for cyan foreground
# 37 for white (or gray) foreground
# 40 for black background
# 41 for red background
# 42 for green background
# 43 for yellow (or brown) background
# 44 for blue background
# 45 for purple background
# 46 for cyan background
# 47 for white (or gray) background

#set cr = "%{\e[31m%}" # red
#set cg = "%{\e[32m%}" # green
#set cy = "%{\e[33m%}" # yellow
#set c0 = "%{\e[0m%}"  # recovery

# prompt
#set promptchars = ">#"
#if ( $?prompt ) then
#    if ( "$uid" == "0" ) then
#        set prompt = "%B$cr%n%b$c0@$cy%m$cg.%? %B$cr%#%b "
#    else
#        set prompt = "%B$cg%n%b$c0@$cy%m$c0.%? %B$cg%#%b "
#    endif
#endif
#set rprompt = "%~"


### prompt ###
set prompt='\
%B%n%b@%U%m%u %B%S%/%s%b\
%B%#%b '
# Prompt used when waiting for stufffor 'for' or 'while'...
set prompt2="%R? "
# Prompt used when correcting.
set prompt3="CORRECT> %B%R%b (y|n|e)? "


### bindkey ###
# Credit: http://www.ibb.net/~anne/keyboard.html
bindkey '\e[1~' beginning-of-line        # Home
bindkey '\e[2~' overwrite-mode           # Insert
bindkey '\e[3~' delete-char              # Delete
bindkey '\e[4~' end-of-line              # End
bindkey '\e[7~' beginning-of-line        # Home (rxvt)
bindkey '\e[8~' end-of-line              # End (rxvt)
bindkey '^W'    backward-delete-word     # Delete
bindkey -k up   history-search-backward  # PageUp
bindkey -k down history-search-forward   # PageDown

# Vi-style key bindings
#bindkey -v


### Tcsh settings ###
set ignoreeof
set listlinks
set nobeep
#set noclobber

set history=512
set savehist=512

# Format of output string when someone logins or logouts
set who="%n has %a %l from %M."

set inputmode=insert

set autolist
set autocorrect

# Use the history of commands to aid expansion.
set autoexpand

set complete=enhance
set correct=cmd

#set autologout = (30 5)
#unset autologout

# Symbolic links are expanded to their true values : chase
# (possible values 'chase' 'ignore' 'expand')
set symlinks=chase

# After a 'Ctrl-Z', list all the jobs
set listjobs

# If the exit value is non-zero, print it
#set printexitvalue

# Ask for confirmation when 'rm *'
set rmstar

# Files that are to be ignored from completion.
set fignore=(.o)

#set time=(8 "\
#Time spent in user mode   (CPU seconds) : %Us\
#Time spent in kernel mode (CPU seconds) : %Ss\
#Total time                              : %Es\
#CPU utilisation (percentage)            : %P\
#Times the process was swapped           : %W\
#Times of major page faults              : %F\
#Times of minor page faults              : %R")

# Before printing prompt after a command, do
#alias precmd date

# Both 'set -n' and set '\003' styles will work.
set echo_style=both


### Aliases ###
#alias rm 'rm -iv'
#alias mv 'mv -iv'
#alias cp 'cp -iv'

alias la    'ls -lAh'
alias ll    'ls -lh'


## Local settings
if ( -e ~/.tcshrc.local ) then
    source ~/.tcshrc.local
endif


# vim: set ts=8 sw=4 tw=0 fenc=utf-8 ft=tcsh: #
