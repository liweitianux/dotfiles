#####################################################################
### /etc/csh.cshrc
### $HOME/.tcshrc
### LY4ever
### June 30, 2011


### "root" environment (basic) ######################################
if ($LOGNAME == "root") then
    set -f path = ( $path /sbin )
    set -f path = ( $path /usr/sbin )
    set -l path = ( $path /usr/local/sbin )
endif

### "ly" environment (basic) ########################################
if ($LOGNAME == "ly") then
    set -f path = ( ~/bin $path )
    set -f path = ( $path /sbin )
    set -f path = ( $path /usr/sbin )
    set -l path = ( $path /usr/local/sbin )
endif

### "user" + "root" environment (basic) #############################
set -f path = ( $path /bin )
set -f path = ( $path /usr/bin )
set -f path = ( $path /usr/local/bin )

### "user" + "root" environment (misc) ##############################
set -l path = ( $path /usr/games )


### Environment settings ############################################
# colors for 'ls'
setenv LSCOLORS     ExGxFxdxCxegedabagExEx
setenv CLICOLOR     yes
# colors for 'grep'
setenv GREP_OPTIONS '--color=auto'

# locale
setenv LANG         en_US.UTF-8
setenv LC_CTYPE     zh_CN.UTF-8
setenv LC_COLLATE   C

# settings for 'fcitx'
setenv XMODIFIERS       @im=fcitx
setenv QT_IM_MODULE     xim
setenv GTK_IM_MODULE    xim

# Misc
setenv PAGER less
setenv LESS '-ceiMs'
setenv EDITOR vim
setenv VISUAL vim


#####################################################################
###  skip remaining setup if not an interactive shell
if ($?USER == 0 || $?prompt == 0) exit


### color code ######################################################
# 1 for brighter colors
# 4 for underlined text
# 5 for flashing text
# 30 for black foreground
# 31 for red foreground
# 32 for green foreground
# 33 for yellow (or brown) foreground
# 34 for blue foreground
# 35 for purple foreground
# 36 for cyan foreground
# 37 for white (or gray) foreground
# 40 for black background
# 41 for red background
# 42 for green background
# 43 for yellow (or brown) background
# 44 for blue background
# 45 for purple background
# 46 for cyan background
# 47 for white (or gray) background

#set cr = "%{\e[31m%}" # red
#set cg = "%{\e[32m%}" # green
#set cy = "%{\e[33m%}" # yellow
#set c0 = "%{\e[0m%}"  # recovery

# prompt
#set promptchars = ">#"
#if ( $?prompt ) then
#    if ( "$uid" == "0" ) then
#        set prompt = "%B$cr%n%b$c0@$cy%m$cg.%? %B$cr%#%b "
#    else
#        set prompt = "%B$cg%n%b$c0@$cy%m$c0.%? %B$cg%#%b "
#    endif
#endif
#set rprompt = "%~"


### prompt ##########################################################
# Source: "UNIX Power Tools" from O'Reilly
# Note from Tim Cera (timcera@sourceforge.net).
set prompt='\
%B%n%b@%U%m%u %B%S%/%s%b\
%B%#%b '
# Prompt used when waiting for stufffor 'for' or 'while'...
set prompt2="%R? "
# Prompt used when correcting.
set prompt3="CORRECT> %B%R%b (y|n|e)? "


### bindkey #########################################################
bindkey '\e[1~' beginning-of-line        # Home
bindkey '\e[3~' delete-char              # Delete
bindkey '\e[4~' end-of-line              # End
bindkey "^W"    backward-delete-word     # Delete
bindkey -k up   history-search-backward  # PageUp
bindkey -k down history-search-forward   # PageDown
# vi-style key bindings
#bindkey -v


### Tcsh settings ###################################################
set ignoreeof
set listlinks
set nobeep
set noclobber

set history = 512
set savehist = 512

# Watch any(1) other users every 0 seconds from any(2) console, 
set watch=(0 any any)

# Format of output string when someone logins or logouts
set who="%n has %a %l from %M."

set inputmode=insert

set autolist
set autocorrect

# Use the history of commands to aid expansion.
set autoexpand

set complete=enhance
set correct=cmd

#set autologout = (30 5)
unset autologout

# Symbolic links are expanded to their true values : chase
# (possible values 'chase' 'ignore' 'expand')
set symlinks=chase

# After a 'Ctrl-Z', list all the jobs
set listjobs

# If the exit value is non-zero, print it
set printexitvalue

# Ask for confirmation when 'rm *'
set rmstar

# Files that are to be ignored from completion.
set fignore=(.o)

set time=(8 "\
Time spent in user mode   (CPU seconds) : %Us\
Time spent in kernel mode (CPU seconds) : %Ss\
Total time                              : %Es\
CPU utilisation (percentage)            : %P\
Times the process was swapped           : %W\
Times of major page faults              : %F\
Times of minor page faults              : %R")

# Before printing prompt after a command, do
#alias precmd date

# Other values are bsd, sysv, etc.
# This way, both 'set -n' and set '\003' styles will work.
set echo_style=both


### Tcsh aliases ####################################################
# for a better habit
#alias rm 'rm -iv'
#alias mv 'mv -iv'
#alias cp 'cp -iv'

alias md    'mkdir -v -p'
alias rd    'rmdir -v'

alias today "date '+%d%h%y'"
alias mess  'clear ; tail -f /var/log/messages'

alias x     'exit'
alias ff    'find . -name $*'

#alias .     'pwd'
alias ..    'cd ..'
alias ../   'cd ../'
alias ...   'cd ../..'
alias .../  'cd ../../'
alias -     'cd -'
alias /     'cd /'
alias ls    'ls -F --color=auto'
alias la    'ls -AF'
alias lla   'ls -AlF'
alias ll    'ls -Flh'
alias lld   'ls -dlh'

alias df    'df -haT'
#alias grep  'grep -i'

# show
alias show-ifstat    'systat -ifstat'      # net interface
alias show-geom      'gstat'               # I/O state
alias show-thermal   'sysctl dev.acpi_ibm.0.thermal'
alias show-cpufreq   'sysctl dev.cpu.0.freq'
alias show-cpulevels 'sysctl dev.cpu.0.freq_levels'
alias show-battery   'sysctl hw.acpi.battery.life && sysctl hw.acpi.battery.time'
alias show-alldep    'make all-depends-list'         # exec under 'ports' dirs
alias show-dep       'portmaster --show-work ./ | sort'

# misc
alias v     'vim'
alias a     'alias'
alias h     'history | less'
alias ox    'oxford'
#alias valgrind-checkmem 'valgrind --tool=memcheck --leak-check=full'  # check 'memory leak'

# aliases for typos
alias maek  'make'
alias alais 'alias'

# for security
alias del   'mv \!* ~/tmp'

### Tcsh completion #################################################
# ~/.tcsh_completion dir
if ( -d ~/.tcsh/completion ) then
    foreach tcsh_comp ( ~/.tcsh/completion/*.tcsh )
        source $tcsh_comp
    end
endif
unset tcsh_comp


### Misc ############################################################
### cdargs
# Just the basic cdargs functionality for tcshell
alias cv 'cdargs \!* && cd `cat $HOME/.cdargsresult`'
# add current directory with description:
alias ca 'cdargs --add=:\!*":"$cwd'
complete cv 'n@*@`cat -s ~/.cdargs | sed -e "s/ .*//"`@'


### Tcsh special aliases ############################################
## tcsh  period
# Every # seconds, do 'command'.
# set tperiod = 2
# alias periodic 'tput bel'
#unset tperiod
#unalias periodic

### ly4ever local ###################################################
## local settings
if ( -e ~/.tcshrc.local ) then
    source ~/.tcshrc.local
endif

## tcsh sched
# sched +8:00 set prompt='Has trabajado mucho. Llama la novia\! :> '

## run commands
#fortune -s | cowsay -f ~/.littleprince.cow -W 48
calendar
date

